plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace = "com.yureitzk.nophotopickerapi"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.yureitzk.nophotopickerapi"
        minSdk = 30
        targetSdk = 36
        versionCode = 1
        versionName = "0.2"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def keystoreFile = System.getenv("KEYSTORE_FILE")
            def keystorePassword = System.getenv("KEYSTORE_PASSWORD")
            def alias = System.getenv("KEY_ALIAS")
            def aliasPass = System.getenv("KEY_PASSWORD")

            if (keystoreFile != null && keystorePassword != null) {
                storeFile file(keystoreFile)
                storePassword keystorePassword
                if (alias != null) keyAlias = alias
                if (aliasPass != null) keyPassword = aliasPass
            }
        }
    }

    buildTypes {
        release {
            def releaseConfig = signingConfigs.findByName("release")
            if (releaseConfig?.storeFile?.exists()) {
                signingConfig = releaseConfig
            }

            minifyEnabled true
            shrinkResources true
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
            packagingOptions {
                resources {
                    excludes += "kotlin/**"
                    excludes += "META-INF/*.version"
                    excludes += "DebugProbesKt.bin"
                    excludes += "kotlin-tooling-metadata.json"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    compileOnly("de.robv.android.xposed:api:82")
}
